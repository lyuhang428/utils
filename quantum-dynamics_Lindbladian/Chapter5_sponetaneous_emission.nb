Direct solve ODE
In[61]:= Clear["`*"]

dagger[mat_]:=ConjugateTranspose[mat];

\[Sigma]x={{0,1},{1,0}};
\[Sigma]y={{0,-I},{I,0}};
\[Sigma]z={{1,0},{0,-1}};
ii=IdentityMatrix[2];
basis={Flatten@ii,Flatten@\[Sigma]x,Flatten@\[Sigma]y,Flatten@\[Sigma]z};

f0={1,0};
\[Rho]0=KroneckerProduct[f0,Conjugate[f0]];
\[Rho]t={{\[Rho]11[t],\[Rho]12[t]},{\[Rho]21[t],\[Rho]22[t]}};
H=\[CapitalDelta]/2*\[Sigma]z;
s={{0,0},{1,0}};
sds=dagger[s].s;

LH=-I*(H.\[Rho]t-\[Rho]t.H);
LL=\[Gamma]/2*(s.\[Rho]t.dagger[s]-1/2(sds.\[Rho]t+\[Rho]t.sds));
L=LH+LL;

res=DSolve[
{\[Rho]11'[t]==L[[1,1]],
\[Rho]12'[t]==L[[1,2]],
\[Rho]21'[t]==L[[2,1]],
\[Rho]22'[t]==L[[2,2]],
\[Rho]11[0]==\[Rho]0[[1,1]],
\[Rho]12[0]==\[Rho]0[[1,2]],
\[Rho]21[0]==\[Rho]0[[2,1]],
\[Rho]22[0]==\[Rho]0[[2,2]]},
{\[Rho]11[t],\[Rho]12[t],\[Rho]21[t],\[Rho]22[t]},t];
res
Out[78]= {{\[Rho]11[t]->E^(-((t \[Gamma])/2)),\[Rho]22[t]->E^(-((t \[Gamma])/2)) (-1+E^((t \[Gamma])/2)),\[Rho]12[t]->0,\[Rho]21[t]->0}}
In[79]:= \[Rho]t={{\[Rho]11[t],\[Rho]12[t]},{\[Rho]21[t],\[Rho]22[t]}}/.res[[1]];
\[Rho]t//MatrixForm
Out[80]//MatrixForm= (E^(-((t \[Gamma])/2))	0
0	E^(-((t \[Gamma])/2)) (-1+E^((t \[Gamma])/2))

)
In[84]:= \[Gamma]=0.25;
Plot[{\[Rho]t[[1,1]],\[Rho]t[[2,2]],\[Rho]t[[1,1]]+\[Rho]t[[2,2]]},{t,0,20},
PlotLegends->LineLegend[{"p0","p1","p0+p1"}],PlotRange->All]
Out[85]= 	p0
	p1
	p0+p1



Liouville space direct solve ODE, standard basis
In[748]:= Clear["`*"];
dagger[mat_]:=ConjugateTranspose[mat];
\[Sigma]x={{0,1},{1,0}};
\[Sigma]y={{0,-I},{I,0}};
\[Sigma]z={{1,0},{0,-1}};
ii=IdentityMatrix[2];
basis=IdentityMatrix[4];

f0={1,0};
\[Rho]0=Flatten@KroneckerProduct[f0,Conjugate[f0]];
\[Rho]t={\[Rho]11[t],\[Rho]12[t],\[Rho]21[t],\[Rho]22[t]};
H=\[CapitalDelta]/2*\[Sigma]z;
s={{0,0},{1,0}};
sds=dagger[s].s;

LH=-I*(KroneckerProduct[H,IdentityMatrix[2]]-KroneckerProduct[IdentityMatrix[2],H]);
LL=\[Gamma]/2*(KroneckerProduct[s,s]-1/2*(KroneckerProduct[sds,IdentityMatrix[2]]+KroneckerProduct[IdentityMatrix[2],sds]));
L=LH+LL;
L\[Rho]=L.\[Rho]t;

res=DSolve[
{\[Rho]11'[t]==L\[Rho][[1]],
\[Rho]12'[t]==L\[Rho][[2]],
\[Rho]21'[t]==L\[Rho][[3]],
\[Rho]22'[t]==L\[Rho][[4]],
\[Rho]11[0]==\[Rho]0[[1]],
\[Rho]12[0]==\[Rho]0[[2]],
\[Rho]21[0]==\[Rho]0[[3]],
\[Rho]22[0]==\[Rho]0[[4]]},
{\[Rho]11[t],\[Rho]12[t],\[Rho]21[t],\[Rho]22[t]},t];
res
Out[766]= {{\[Rho]11[t]->E^(-((t \[Gamma])/2)),\[Rho]22[t]->E^(-((t \[Gamma])/2)) (-1+E^((t \[Gamma])/2)),\[Rho]12[t]->0,\[Rho]21[t]->0}}
In[767]:= \[Rho]t=Partition[{\[Rho]11[t],\[Rho]12[t],\[Rho]21[t],\[Rho]22[t]}/.res[[1]],2];
\[Rho]t2=\[Rho]t.\[Rho]t;
tr2=Tr@\[Rho]t2;
\[Rho]t//MatrixForm
Out[770]//MatrixForm= (E^(-((t \[Gamma])/2))	0
0	E^(-((t \[Gamma])/2)) (-1+E^((t \[Gamma])/2))

)
In[774]:= \[Gamma]=0.25;
\[CapitalDelta]=1;
Plot[{\[Rho]t[[1,1]],\[Rho]t[[2,2]],\[Rho]t[[1,1]]+\[Rho]t[[2,2]],tr2},{t,0,50},
PlotRange->All,PlotLegends->LineLegend[{"p0","p1","p0+p1","tr2"}]]
Out[776]= 	p0
	p1
	p0+p1
	tr2


Liouville space direct solve ODE, Pauli basis
In[368]:= Clear["`*"];
dagger[mat_]:=ConjugateTranspose[mat];
\[Sigma]x={{0,1},{1,0}};
\[Sigma]y={{0,-I},{I,0}};
\[Sigma]z={{1,0},{0,-1}};
ii=IdentityMatrix[2];
basis={Flatten@ii,Flatten@\[Sigma]x,Flatten@\[Sigma]y,Flatten@\[Sigma]z};

f0={1,0};
\[Rho]0={1/2,0,0,1/2};(* 0.5I+0.5\[Sigma]z *)
\[Rho]t={\[Rho]ii[t],\[Rho]x[t],\[Rho]y[t],\[Rho]z[t]};
H=\[CapitalDelta]/2*\[Sigma]z;
s={{0,0},{1,0}};
sds=dagger[s].s;

LH=-I*(KroneckerProduct[H,IdentityMatrix[2]]-KroneckerProduct[IdentityMatrix[2],H]);
LL=\[Gamma]/2*(KroneckerProduct[s,s]-1/2*(KroneckerProduct[sds,IdentityMatrix[2]]+KroneckerProduct[IdentityMatrix[2],sds]));
unitary=Table[0,{i,4},{j,4}];
dissipator=Table[0,{i,4},{j,4}];
Do[
Do[
unitary[[i,j]]=Conjugate@basis[[i]].LH.basis[[j]]/2;
dissipator[[i,j]]=Conjugate[basis[[i]]].LL.basis[[j]]/2;
,{j,4}];
,{i,4}];
L=unitary+dissipator;
L\[Rho]=L.\[Rho]t;

res=DSolve[
{\[Rho]ii'[t]==L\[Rho][[1]],
\[Rho]x'[t]==L\[Rho][[2]],
\[Rho]y'[t]==L\[Rho][[3]],
\[Rho]z'[t]==L\[Rho][[4]],
\[Rho]ii[0]==\[Rho]0[[1]],
\[Rho]x[0]==\[Rho]0[[2]],
\[Rho]y[0]==\[Rho]0[[3]],
\[Rho]z[0]==\[Rho]0[[4]]},
{\[Rho]ii[t],\[Rho]x[t],\[Rho]y[t],\[Rho]z[t]},t];
res
Out[389]= {{\[Rho]ii[t]->1/2,\[Rho]z[t]->-(1/2) E^(-((t \[Gamma])/2)) (-2+E^((t \[Gamma])/2)),\[Rho]x[t]->0,\[Rho]y[t]->0}}
In[392]:= \[Rho]t=\[Rho]ii[t]*ii+\[Rho]x[t]*\[Sigma]x+\[Rho]y[t]*\[Sigma]y+\[Rho]z[t]*\[Sigma]z/.res[[1]];
\[Rho]t//MatrixForm//FullSimplify
Out[393]//MatrixForm= (E^(-((t \[Gamma])/2))	0
0	1-E^(-((t \[Gamma])/2))

)
In[394]:= \[Gamma]=0.25;
\[CapitalDelta]=1;
Plot[{\[Rho]t[[1,1]],\[Rho]t[[2,2]],\[Rho]t[[1,1]]+\[Rho]t[[2,2]]},{t,0,20},
PlotRange->All,PlotLegends->LineLegend[{"p0","p1","p0+p1"}]]
Out[396]= 	p0
	p1
	p0+p1


Liouvillian superoperator
In[419]:= Clear["`*"];
dagger[mat_]:=ConjugateTranspose[mat];
\[Sigma]x={{0,1},{1,0}};
\[Sigma]y={{0,-I},{I,0}};
\[Sigma]z={{1,0},{0,-1}};
ii=IdentityMatrix[2];
basis=IdentityMatrix[4];

f0={1,0};
\[Rho]0={1,0,0,0};
\[Rho]t={\[Rho]11[t],\[Rho]12[t],\[Rho]21[t],\[Rho]22[t]};
H=\[CapitalDelta]/2*\[Sigma]z;
s={{0,0},{1,0}};
sds=dagger[s].s;

LH=-I*(KroneckerProduct[H,IdentityMatrix[2]]-KroneckerProduct[IdentityMatrix[2],H]);
LL=\[Gamma]/2*(KroneckerProduct[s,s]-1/2*(KroneckerProduct[sds,IdentityMatrix[2]]+KroneckerProduct[IdentityMatrix[2],sds]));
L=LH+LL;
U=MatrixExp[L*t];

\[Rho]t=Partition[U.\[Rho]0,2];
\[Rho]t//MatrixForm
Out[437]//MatrixForm= (E^(-((t \[Gamma])/2))	0
0	E^(-((t \[Gamma])/2)) (-1+E^((t \[Gamma])/2))

)
In[438]:= \[Gamma]=0.25;
\[CapitalDelta]=1;
Plot[{\[Rho]t[[1,1]],\[Rho]t[[2,2]],\[Rho]t[[1,1]]+\[Rho]t[[2,2]]},{t,0,20},
PlotRange->All,PlotLegends->LineLegend[{"p0","p1","p0+p1"}]]
Out[440]= 	p0
	p1
	p0+p1

